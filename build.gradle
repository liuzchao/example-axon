ext {
    aspectjVersion = '1.7.4'
    axonVersion = '2.1.2'
    groovyVersion = '2.2.2'
    springVersion = '4.0.3.RELEASE'
}

allprojects {
    group = 'example-axon'
    version = '1.0'

    apply plugin: 'java'

    repositories {
        mavenCentral()
    }
}

configure(subprojects.findAll { it.name =~ '-api' }) {
    apply plugin: 'groovy'

    dependencies {
        compile("org.codehaus.groovy:groovy-all:${groovyVersion}")
    }
}

configure(subprojects.findAll { it.name =~ '-impl' }) {
    dependencies {
        compile("org.springframework:spring-webmvc:${springVersion}")

        compile("org.axonframework:axon-core:${axonVersion}")
        compile("org.axonframework:axon-amqp:${axonVersion}")
    }
}

project(':management-impl') {
    dependencies {
        compile project(':management-api')
    }
}

project(':payments-impl') {
    dependencies {
        compile project(':payments-api')
    }
}

project(':registrations-impl') {
    dependencies {
        compile project(':management-api')
        compile project(':payments-api')
        compile project(':registrations-api')
    }
}

project(':application') {
    apply plugin: 'spring-boot'

    dependencies {
        compile project(':management-impl')
        compile project(':payments-impl')
        compile project(':registrations-impl')

        compile('org.springframework.boot:spring-boot-starter-web')
        compile('org.springframework.boot:spring-boot-starter-amqp')
        testCompile('org.springframework.boot:spring-boot-starter-test')

        compile("org.aspectj:aspectjweaver:${aspectjVersion}")

        compile("org.springframework:spring-aop:${springVersion}")
        compile("org.springframework:spring-tx:${springVersion}")
    }
}

buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-snapshot" }
        mavenLocal()
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.0.2.RELEASE')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
