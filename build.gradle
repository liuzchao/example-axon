ext {
    aspectjVersion = '1.7.4'
    axonVersion = '2.1.1'
    groovyVersion = '2.2.2'
    springVersion = '4.0.3.RELEASE'
}

allprojects {
    group = 'example-axon'
    version = '1.0'

    apply plugin: 'java'

    repositories {
        mavenCentral()
    }
}

configure(subprojects.findAll { it.name =~ '-api' }) {
    apply plugin: 'groovy'

    dependencies {
        compile("org.codehaus.groovy:groovy-all:${groovyVersion}")
    }
}

configure(subprojects.findAll { it.name =~ '-domain' }) {
    dependencies {
        compile("org.springframework:spring-webmvc:${springVersion}")

        compile("org.aspectj:aspectjweaver:${aspectjVersion}")

        compile("org.axonframework:axon-core:${axonVersion}")
    }
}

project(':management-domain') {
    dependencies {
        compile project(':management-api')
    }
}

project(':payments-domain') {
    dependencies {
        compile project(':payments-api')
    }
}

project(':registrations-domain') {
    dependencies {
        compile project(':management-api')
        compile project(':payments-api')
        compile project(':registrations-api')
    }
}

project(':application') {
    apply plugin: 'spring-boot'

    dependencies {
        compile project(':management-domain')
        compile project(':payments-domain')
        compile project(':registrations-domain')

        compile('org.springframework.boot:spring-boot-starter-web')
        testCompile('org.springframework.boot:spring-boot-starter-test')

        compile("org.springframework:spring-aop:${springVersion}")
        compile("org.springframework:spring-tx:${springVersion}")
    }
}

buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-snapshot" }
        mavenLocal()
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.0.2.RELEASE')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
