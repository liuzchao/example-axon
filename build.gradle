ext {
    axonVersion = '2.1.2'
    groovyVersion = '2.2.2'
    springVersion = '4.0.3.RELEASE'
}

allprojects {
    group = 'example-axon'
    version = '1.0'

    repositories {
        mavenCentral()
    }
}

project(':conference-client') {
    apply plugin: 'groovy'

    dependencies {
        compile("org.codehaus.groovy:groovy-all:${groovyVersion}")
        compile("com.jayway.restassured:rest-assured:2.3.1")
    }
}

project(':conference-configuration') {
    apply plugin: 'java'

    dependencies {
        compile("org.springframework:spring-context:${springVersion}")

        compile("org.axonframework:axon-core:${axonVersion}")
        compile("org.axonframework:axon-amqp:${axonVersion}")
    }
}

configure(subprojects.findAll { it.name =~ '-api' }) {
    apply plugin: 'groovy'

    dependencies {
        compile("org.codehaus.groovy:groovy-all:${groovyVersion}")
    }
}

configure(subprojects.findAll { it.name =~ '-impl' }) {
    apply plugin: 'java'
    apply plugin: 'spring-boot'

    dependencies {
        compile project(':conference-configuration')

        compile project(':management-api')
        compile project(':payments-api')
        compile project(':registrations-api')

        compile('org.springframework.boot:spring-boot-starter-amqp')
        compile('org.springframework.boot:spring-boot-starter-web')
    }
}

project(':management-impl') {
    dependencies {
        compile("org.springframework.boot:spring-boot-starter-data-mongodb")
        compile("org.springframework.boot:spring-boot-starter-data-rest")
    }
}

project(':payments-impl') {
    dependencies {
    }
}

project(':registrations-impl') {
    dependencies {
    }
}

buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-snapshot" }
        mavenLocal()
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.0.2.RELEASE')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
