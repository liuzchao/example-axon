buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-snapshot" }
        mavenLocal()
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.0.2.RELEASE')
    }
}

configure(allprojects) { project ->
    group = 'example-axon'
    version = '1.0'

    ext {
        axonVersion = '2.1.2'
        groovyVersion = '2.3.0'
        springVersion = '4.0.3.RELEASE'
        springAmqpVersion = '1.2.1.RELEASE'
        springBootVersion = '1.0.2.RELEASE'
    }

    repositories {
        mavenCentral()
    }

    apply plugin: "java"

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

configure(subprojects.findAll { it.name =~ '-api' }) {
    apply plugin: 'groovy'

    dependencies {
        compile project(':conference-shared')

        compile("org.codehaus.groovy:groovy-all:${groovyVersion}")
    }
}

configure(subprojects.findAll { it.name =~ '-impl' }) {
    apply plugin: 'spring-boot'

    dependencies {
        compile project(':conference-shared')
        compile project(':conference-configuration')

        compile project(':management-api')
        compile project(':payments-api')
        compile project(':registrations-api')

        compile('org.springframework.boot:spring-boot-starter-web')
    }
}


project(':conference-client') {
    apply plugin: 'groovy'

    dependencies {
        compile("org.codehaus.groovy:groovy-all:${groovyVersion}")
        compile("com.jayway.restassured:rest-assured:2.3.1") {
            exclude group: "org.codehaus.groovy", module: 'groovy'
        }
    }
}

project(':conference-configuration') {
    dependencies {
        compile("org.springframework:spring-context:${springVersion}")
        compile("org.springframework.amqp:spring-rabbit:${springAmqpVersion}")
        compile("org.springframework.boot:spring-boot:${springBootVersion}")

        compile("org.axonframework:axon-core:${axonVersion}")
        compile("org.axonframework:axon-amqp:${axonVersion}")
    }
}

project(':conference-shared') {
    apply plugin: 'groovy'

    dependencies {
        compile("org.codehaus.groovy:groovy-all:${groovyVersion}")
    }
}

project(':management-impl') {
    dependencies {
        compile("org.springframework.boot:spring-boot-starter-data-jpa")
        compile("org.springframework.boot:spring-boot-starter-data-rest")
        compile("com.h2database:h2")
    }
}

project(':payments-impl') {
    dependencies {
        // TODO
    }
}

project(':registrations-impl') {
    dependencies {
        // TODO
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
